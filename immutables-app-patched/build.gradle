apply plugin: 'org.javamodularity.moduleplugin'

dependencies {
    constraints {
	   // override mongo legacy driver 4.3.1 transiently referenced by immutables with invalid Automatic-Module-Name   
	   implementation "org.mongodb:mongodb-driver-legacy:4.4.1"
	}
  
    implementation( "org.immutables:value:2.9.0",
    				"org.immutables:mongo:2.9.0",
    				// javax.inject with JPMS support
  					"jakarta.inject:jakarta.inject-api:1.0.3",
  					// java.annotation
			        "javax.annotation:javax.annotation-api:1.3.2",
			        // java.validation
    				"javax.validation:validation-api:2.0.1.Final",
    				// package javax.validation.concurrent (to be patched to java.validation) 
    				"com.google.code.findbugs:jsr305:3.0.2",
    				// logging
					"org.slf4j:slf4j-api:1.7.32",
					"ch.qos.logback:logback-classic:1.2.9"
    
    )
    
  // Use JUnit Jupiter for testing.
  testImplementation 'org.junit.jupiter:junit-jupiter:5.7.2'

  annotationProcessor( "org.immutables:value:2.9.0",
  					   "org.immutables:mongo:2.9.0",
                       "org.immutables:criteria-inmemory:2.9.0"
  )
}

compileJava {
    moduleOptions {
        addExports = [
            "java.annotation/javax.annotation.concurrent": moduleName
        ]
    }
}

// fix jsr305 split packages by patching it to java.annoation module 
modularity.patchModule("java.annotation", "jsr305-3.0.2.jar")

//fix mongo legacy driver (can be removed after CRB-1191) 
modularity.patchModule("org.mongodb.driver.core", "mongodb-driver-legacy-4.4.1.jar")

// apply module fixes to Eclipse classpath file as well 
modularity.improveEclipseClasspathFile()

// make the immutables processor generated build-subfolder a source-folder in Eclipse
eclipse {
    classpath {
        file.beforeMerged { cp ->
            cp.entries.add( new org.gradle.plugins.ide.eclipse.model.SourceFolder('build/generated/sources/annotationProcessor/java/main', null) )
        }
    }
}
